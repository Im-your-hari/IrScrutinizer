<?xml version="1.0" encoding="UTF-8"?>
<!--
Copyright (C) 2016 Bengt Martensson

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.
-->

<exportformats:exportformat xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance"
                            xmlns:exportformats="http://www.harctoolbox.org/exportformats"
                            xsi:schemaLocation="http://www.harctoolbox.org/exportformats http://www.harctoolbox.org/schemas/exportformats.xsd"
                            name="RemoteMaster" extension="rmdu" multiSignal="true" simpleSequence="false" metadata="true" encoding="us-ascii">
<xsl:stylesheet xmlns:girr="http://www.harctoolbox.org/Girr"
                xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:java="http://xml.apache.org/xalan/java" version="1.0">
    <xsl:output method="text" encoding="us-ascii"/>

    <xsl:template match="/">
        <xsl:apply-templates select="girr:remotes/girr:remote[1]"/> <!-- just consider the first remote -->
    </xsl:template>

    <xsl:variable name="protocols" select="//girr:parameters/@protocol" />
    <xsl:key name="protocol" match="girr:parameters/@protocol" use="." />

    <xsl:template name="unique-protocols">
        <xsl:for-each select="$protocols">
            <xsl:if test="generate-id() = generate-id($protocols[. = current()][1])">
                <xsl:text> </xsl:text>
                <xsl:value-of select="."/>
            </xsl:if>
        </xsl:for-each>
    </xsl:template>

    <xsl:template match="girr:remote">
        <xsl:text># This file was created by IrScrutinizer.
# It is not directly usable in RemoteMaster. For this, make sure that
# protocols.ini contains the "protocol" ImportOnly. Also, check
# Options -> "Allow preserve Control", and then select
# "Preserve: OBC and function parameters" on the "Setup" pane.
# Then open this file with RemoteMaster, and select a sensible executor
# (called protocol in RemoteMaster).
#
# This file is strictly US-ASCII.
#
# Original protocol(s):</xsl:text>
        <xsl:call-template name="unique-protocols"/>
        <xsl:text>
#
Description=</xsl:text>
<xsl:if test="@name = ''">
    <xsl:value-of select="@displayName"/>
</xsl:if>
<xsl:if test="@name != ''">
    <xsl:value-of select="@name"/>
</xsl:if>
<xsl:text>
Notes=\
Creating tool: </xsl:text>
<xsl:value-of select="$creatingTool"/>
<xsl:text>\n\Creating user: </xsl:text>
<xsl:value-of select="$creatingUser"/>
<xsl:text>\n\
Creating date: </xsl:text>
<xsl:value-of select="$creatingDate"/>
<xsl:text>\n\nManufacturer: </xsl:text>
<xsl:value-of select="@manufacturer"/>
<xsl:text>\nModel: </xsl:text>
<xsl:value-of select="@model"/>
<xsl:text>\nDevice Class: </xsl:text>
<xsl:value-of select="@deviceClass"/>
<xsl:text>\nRemotename: </xsl:text>
<xsl:value-of select="@remoteName"/>
<xsl:text>\n\nOriginal protocol(s):</xsl:text>
<xsl:call-template name="unique-protocols"/>
<xsl:text>
Remote.name=URC-7781 One For All Digital 12
Remote.signature=11311131
DeviceType=Cable
DeviceIndex=3
SetupCode=1999
Protocol=99 99
Protocol.name=ImportOnly
ProtocolParms=</xsl:text>
        <xsl:value-of select="girr:commandSet[1]/girr:command[1]/girr:parameters/girr:parameter[@name='D']/@value"/>
        <xsl:text> </xsl:text>
        <xsl:value-of select="girr:commandSet[1]/girr:command[1]/girr:parameters/girr:parameter[@name='S']/@value"/>
        <xsl:text>
</xsl:text>
        <xsl:apply-templates select="girr:commandSet/girr:command"/>
    </xsl:template>

    <xsl:template match="girr:command">
        <xsl:text>Function.</xsl:text>
        <xsl:value-of select="position() - 1"/>
        <xsl:text>.name=</xsl:text>
        <xsl:value-of select="@name"/>
        <xsl:text>
Function.</xsl:text>
        <xsl:value-of select="position() - 1"/>
        <xsl:text>.hex=</xsl:text>
        <xsl:value-of select="java:java.lang.Integer.toHexString(number(girr:parameters/girr:parameter[@name='F']/@value))"/>
        <xsl:apply-templates select="@comment"/>
        <xsl:text>
</xsl:text>

    </xsl:template>

    <xsl:template match="@comment">
        <xsl:text>
Function.</xsl:text>
        <xsl:value-of select="count(../preceding-sibling::girr:command)"/>
        <xsl:text>.notes=</xsl:text>
        <xsl:value-of select="."/>
    </xsl:template>
</xsl:stylesheet>
</exportformats:exportformat>
